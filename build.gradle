import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "io.freefair.lombok" version "6.3.0" apply false
    id "com.github.johnrengelman.shadow" version "7.1.0" apply false
    id "com.sidneysimmons.gradle-plugin-external-properties" version "2.0.0"
}

externalProperties { propertiesFileResolver project.file("secrets.properties") }

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://libraries.minecraft.net" }
        maven { url = uri('https://papermc.io/repo/repository/maven-public/') }
        maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
        maven { url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/') }
        maven { url = uri("https://repo.rapture.pw/repository/maven-releases/") }
        maven { url = uri("https://repo.rapture.pw/repository/maven-snapshots/") }
        maven { url = uri('https://repo.kryptonmc.org/releases') }
        maven { url = uri('https://repo.codemc.io/repository/maven-public/') }
        maven {
            url = "https://nexus.mooncraft.dev/repository/spigots/"
            credentials.username = props.get("nexus-username", properties.get("nexus-username") as String)
            credentials.password = props.get("nexus-password", properties.get("nexus-password") as String)
        }
        maven {
            url = "https://nexus.mooncraft.dev/repository/mooncraft-releases/"
            credentials.username = props.get("nexus-username", properties.get("nexus-username") as String)
            credentials.password = props.get("nexus-password", properties.get("nexus-password") as String)
        }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.freefair.lombok"
    apply plugin: "com.github.johnrengelman.shadow"

    lombok { version = "1.18.22" }

    sourceSets {
        main.java.srcDirs = ['src']
        main.resources.srcDirs = ['resources']
    }

    java {
        withSourcesJar()
        withJavadocJar()
        sourceCompatibility = JavaVersion.VERSION_16
    }

    processResources {
        filter ReplaceTokens, tokens: [
                "plugin-author"     : project.property("plugin-author"),
                "plugin-version"    : project.property("plugin-version"),
                "plugin-api-version": project.property("plugin-api-version")
        ]
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
        annotationProcessor 'org.jetbrains:annotations:23.0.0'
    }

    jar {
        archiveClassifier.set("base")
    }
    sourcesJar {
        archiveClassifier.set("sources")
    }
    javadocJar {
        archiveClassifier.set("javadocs")
    }
    shadowJar {
        archiveClassifier.set("")
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }
}

task buildBase(type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar).named("jar") }
    into "/COMPILED-JARS/base"
}
task buildSources(type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar).named("sourcesJar") }
    into "/COMPILED-JARS/sources"
}
task buildJavadoc(type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar).named("javadocJar") }
    into "/COMPILED-JARS/javadocs"
}
task buildShadowjar(type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar).named("shadowJar") }
    exclude('data.jar')
    exclude('common.jar')
    into "/COMPILED-JARS"
}
task buildAll(type: Copy) {
    dependsOn clean
    dependsOn buildBase
    dependsOn buildJavadoc
    dependsOn buildSources
    dependsOn buildShadowjar
}